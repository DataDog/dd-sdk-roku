' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

import "pkg:/source/datadogSdk.bs"
import "pkg:/source/internalLogger.bs"
import "pkg:/source/timeUtils.bs"
import "pkg:/source/logs/logStatus.bs"

' *****************************************************************
' * RumAgent: a background component listening for internal events
' *     to write relevant RUM Event to Datadog.
' *****************************************************************

' ----------------------------------------------------------------
' Initialize the component
' ----------------------------------------------------------------
sub init()
    ddlogThread("LogsAgent.init()")
    m.applicationVersion = CreateObject("roAppInfo").GetVersion()
end sub

' ----------------------------------------------------------------
' Adds a ok log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logOk(message as string, attributes as object)
    ddlogThread("LogsAgent.logOk()")
    sendLog(LogStatus.ok, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a debug log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logDebug(message as string, attributes as object)
    ddlogThread("LogsAgent.logDebug()")
    sendLog(LogStatus.debug, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a info log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logInfo(message as string, attributes as object)
    ddlogThread("LogsAgent.logInfo()")
    sendLog(LogStatus.info, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a notice log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logNotice(message as string, attributes as object)
    ddlogThread("LogsAgent.logNotice()")
    sendLog(LogStatus.notice, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a warn log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logWarn(message as string, attributes as object)
    ddlogThread("LogsAgent.logWarn()")
    sendLog(LogStatus.warn, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a error log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logError(message as string, attributes as object)
    ddlogThread("LogsAgent.logError()")
    sendLog(LogStatus.error, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a critical log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logCritical(message as string, attributes as object)
    ddlogThread("LogsAgent.logCritical()")
    sendLog(LogStatus.critical, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a alert log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logAlert(message as string, attributes as object)
    ddlogThread("LogsAgent.logAlert()")
    sendLog(LogStatus.alert, message, attributes)
end sub

' ----------------------------------------------------------------
' Adds a emergency log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub logEmergency(message as string, attributes as object)
    ddlogThread("LogsAgent.logEmergency()")
    sendLog(LogStatus.emergency, message, attributes)
end sub

' ----------------------------------------------------------------
' Sends a log event
' @param status (LogStatus) the status of the log
' @param message (string) the log message
' @param attributes (object) additional custom attributes
' ----------------------------------------------------------------
sub sendLog(status as LogStatus, message as string, attributes as object)
    timestamp& = getTimestamp()
    ensureSetup()
    logEvent = {
        date: timestamp&,
        ddtags: "env:" + m.top.envName + ",version:" + m.applicationVersion,
        message: message,
        status: status,
        service: m.top.serviceName,
        usr: m.global.datadogUserInfo
    }
    for each key in attributes
        logEvent[key] = attributes[key]
    end for
    for each key in m.global.datadogContext
        logEvent[key] = m.global.datadogContext[key]
    end for
    m.writer.writeEvent = FormatJson(logEvent)
end sub

' ----------------------------------------------------------------
' Ensure all dependencies are present (from DI or generated)
' ----------------------------------------------------------------
sub ensureSetup()
    ensureUploader()
    ensureWriter()
end sub

' ----------------------------------------------------------------
' Sets the uploader node from the top node's field,
' or instantiate one.
' ----------------------------------------------------------------
sub ensureUploader()
    if (m.uploader = invalid)
        if (m.top.uploader <> invalid)
            m.uploader = m.top.uploader
        else
            m.uploader = CreateObject("roSGNode", "UploaderTask")
        end if

        ' Configure uploader
        m.uploader.endpointHost = m.top.endpointHost
        m.uploader.trackType = TrackType.logs
        m.uploader.payloadPrefix = "["
        m.uploader.payloadPostfix = "]"
        m.uploader.clientToken = m.top.clientToken
        m.uploader.contentType = "application/json"
        m.uploader.queryParams = { ddsource: agentSource() }
    end if
end sub

' ----------------------------------------------------------------
' Sets the writer node from the top node's field,
' or instantiate one.
' ----------------------------------------------------------------
sub ensureWriter()
    if (m.writer = invalid)
        if (m.top.writer <> invalid)
            m.writer = m.top.writer
        else
            m.writer = CreateObject("roSGNode", "WriterTask")
        end if

        ' Configure writer
        m.writer.trackType = TrackType.logs
        m.writer.payloadSeparator = ","
    end if
end sub
