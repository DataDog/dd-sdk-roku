' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

import "pkg:/source/rum/rumRawEvents.bs"
import "pkg:/source/datadogSdk.bs"
import "pkg:/source/internalLogger.bs"

'*****************************************************************
' * RumAgent: a background component listening for internal events
' *     to write relevant RUM Event to Datadog.
'*****************************************************************

' ----------------------------------------------------------------
' Initialize the component
' ----------------------------------------------------------------
sub init()
    m.port = createObject("roMessagePort")
    m.top.observeFieldScoped("stop", m.port)
    m.top.functionName = "rumAgentLoop"
    m.top.control = "RUN"
end sub

' ----------------------------------------------------------------
' Main RumAgent loop
' ----------------------------------------------------------------
sub rumAgentLoop()
    logInfo("Starting main RumAgent loop")

    while(true)
        msg = wait(m.top.keepAliveDelayMs, m.port)
        ensureSetup()
        m.rumScope@.handleEvent(keepAliveEvent(), m.writer)
        msgType = type(msg)
        if (msgType = "roSGNodeEvent")
            fieldName = msg.getField()
            if (fieldName = "stop")
                return
            else
                logWarning(fieldName + " not handled")
            end if
        end if
    end while
end sub

' ----------------------------------------------------------------
' Starts a view
' @param name (string) the view name (human-readable)
' @param url (string) the view url (developer identifier)
' ----------------------------------------------------------------
sub startView(name as string, url as string)
    ensureSetup()
    m.rumScope@.handleEvent(startViewEvent(name, url), m.writer)
end sub

' ----------------------------------------------------------------
' Stops a view
' @param name (string) the view name (human-readable)
' @param url (string) the view url (developer identifier)
' ----------------------------------------------------------------
sub stopView(name as string, url as string)
    ensureSetup()
    m.rumScope@.handleEvent(stopViewEvent(name, url), m.writer)
end sub

' ----------------------------------------------------------------
' Adds an action
' @param action (object) the action to track
' ----------------------------------------------------------------
sub addAction(action as object)
    ensureSetup()
    m.rumScope@.handleEvent(addActionEvent(action), m.writer)
end sub

' ----------------------------------------------------------------
' Adds an error
' @param exception (object) the caught exception object
' ----------------------------------------------------------------
sub addError(exception as object)
    ensureSetup()
    m.rumScope@.handleEvent(addErrorEvent(exception), m.writer)
end sub

' ----------------------------------------------------------------
' Adds a resource
' @param resource (object) the tracked resource object (as retrieved from the roSystemLog)
' ----------------------------------------------------------------
sub addResource(resource as object)
    ensureSetup()
    m.rumScope@.handleEvent(addResourceEvent(resource), m.writer)
end sub

' ----------------------------------------------------------------
' Ensure all dependencies are present (from DI or generated)
' ----------------------------------------------------------------
sub ensureSetup()
    ensureRumScope()
    ensureUploader()
    ensureWriter()
end sub

' ----------------------------------------------------------------
' Sets the uploader node from the top node's field,
' or instantiate one.
' ----------------------------------------------------------------
sub ensureRumScope()
    if (m.rumScope = invalid)
        if (m.top.rumScope <> invalid)
            m.rumScope = m.top.rumScope
        else
            m.rumScope = CreateObject("roSGNode", "RumApplicationScope")
        end if
        m.rumScope.applicationId = m.top.applicationId
        m.rumScope.serviceName = m.top.serviceName
    end if
end sub

' ----------------------------------------------------------------
' Sets the uploader node from the top node's field,
' or instantiate one.
' ----------------------------------------------------------------
sub ensureUploader()
    if (m.uploader = invalid)
        if (m.top.uploader <> invalid)
            m.uploader = m.top.uploader
        else
            m.uploader = CreateObject("roSGNode", "UploaderTask")
        end if

        ' Configure uploader
        m.uploader.endpointHost = m.top.endpointHost
        m.uploader.trackType = TrackType.rum
        m.uploader.payloadPrefix = ""
        m.uploader.payloadPostfix = ""
        m.uploader.clientToken = m.top.clientToken
    end if
end sub

' ----------------------------------------------------------------
' Sets the writer node from the top node's field,
' or instantiate one.
' ----------------------------------------------------------------
sub ensureWriter()
    if (m.writer = invalid)
        if (m.top.writer <> invalid)
            m.writer = m.top.writer
        else
            m.writer = CreateObject("roSGNode", "WriterTask")
        end if

        ' Configure writer
        m.writer.trackType = TrackType.rum
        m.writer.payloadSeparator = chr(10)
    end if
end sub
