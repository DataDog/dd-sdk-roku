' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

sub init()
    m.top.functionName = "performRequest"
end sub

sub performRequest()
    datadogroku_ddLogThread("RequestTask.performRequest()")

    m.port = CreateObject("roMessagePort")

    requestUrl = "https://www.google.com/search?q=" + m.top.query
    if (Rnd(2) = 1)
        requestUrl = "https://www.google.com/notasearch?q=" + m.top.query
    end if

    request = CreateObject("roUrlTransfer")
    request.SetUrl(requestUrl)
    request.RetainBodyOnError(true)
    request.EnablePeerVerification(false)
    request.EnableHostVerification(false)
    request.SetMessagePort(m.port)

    timer = CreateObject("roTimespan")
    timer.Mark()
    request.AsyncGetToString()

    while (true)
        msg = wait(5000, m.port)
        if (msg <> invalid)
            msgType = type(msg)
            if (msgType = "roUrlEvent")
                if (msg.GetInt() = 1) ' transfer complete
                    durationMs& = timer.TotalMilliseconds()
                    transferTime# = datadogroku_millisToSec(durationMs&)

                    httpCode = msg.GetResponseCode()
                    status = "ok"
                    if (httpCode < 0)
                        status = msg.GetFailureReason()
                    end if
                    resource = {
                        url: requestUrl,
                        method: "GET",
                        transferTime: transferTime#,
                        httpCode: httpCode,
                        status: status
                    }
                    m.global.datadogRumAgent@.addResource(resource)
                end if
            end if
        end if
    end while
end sub
