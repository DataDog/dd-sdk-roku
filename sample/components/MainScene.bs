' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

import "pkg:/source/exploding.bs"
import "pkg:/source/roku_modules/datadogroku/datadogSdk.brs"
import "pkg:/source/roku_modules/datadogroku/internalLogger.brs"
import "pkg:/source/roku_modules/datadogroku/timeUtils.brs"

sub init()
    datadogroku_ddLogThread("MainScene.init()")
    m.top.backgroundURI = "pkg:/images/main_bg_hd.jpg"
    m.top.setFocus(true)

    m.videoScreen = m.top.findNode("videoScreen")
    m.debugScreen = m.top.findNode("debugScreen")

    credentials = parseJson(readAsciiFile("pkg:/credentials.json"))
    datadogroku_initialize({
        clientToken: credentials.datadogClientToken,
        applicationId: credentials.datadogApplicationId,
        site: "us1",
        service: "roku-sample",
        env: "prod"
    })

    m.global.setField("datadogUserInfo", { id: 42, name: "Abcd Efg", email: "abcd.efg@example.com" })
    m.global.setField("datadogContext", { channel: "roku sample channel" })
    m.global.setField("datadogVerbosity", 5)

    switchScreen()
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    datadogroku_ddLogVerbose("MainScreen.onKeyEvent(" + key + ", " + press.toStr() + ")")
    if ((key = "options" or key = "back") and press)
        m.global.datadogRumAgent@.addAction({ target: "options", type: "click" })
        switchScreen()
        return true
    end if
    return false
end function

sub switchScreen()
    if (m.debugScreen.visible)
        m.global.datadogLogsAgent@.logOk("Switching screen to video", { screen: "Debug" })
        m.global.datadogRumAgent@.startView("VideoScreen", "components/screens/VideoScreen.xml")
        m.debugScreen.visible = false
        m.debugScreen.setFocus(false)
        m.videoScreen.visible = true
        m.videoScreen.setFocus(true)
    else
        m.global.datadogLogsAgent@.logOk("Switching screen to debug", { screen: "Video" })
        m.global.datadogRumAgent@.startView("DebugScreen", "components/screens/DebugScreen.xml")
        m.videoScreen.visible = false
        m.videoScreen.setFocus(false)
        m.debugScreen.visible = true
        m.debugScreen.setFocus(true)
    end if
end sub
