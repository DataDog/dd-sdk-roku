' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

' ----------------------------------------------------------------
' Main entry point for the sample app
' @param args (dynamic) arguments passed by the OS when starting the channel
' ----------------------------------------------------------------
sub RunUserInterface(args as dynamic)
    if ((args.mediaType <> invalid) and (args.contentId <> invalid))
        print "Sample app launched with deeplink: " + args.contentId + "/" + args.mediaType
    end if

    screen = CreateObject("roSGScreen")

    m.global = screen.getGlobalNode()

    m.port = CreateObject("roMessagePort")

    screen.setMessagePort(m.port)
    scene = screen.CreateScene("MainScene")
    screen.show()
    scene.signalBeacon("AppLaunchComplete")

    ' Handle roInput (deeplink changes at runtime)
    input = CreateObject("roInput")
    input.setMessagePort(m.port)

    ' Handle roSystemLogEvent
    sysLog = CreateObject("roSystemLog")
    sysLog.setMessagePort(m.port)
    sysLog.enableType("http.error")
    sysLog.enableType("http.complete")

    while(true)
        msg = wait(0, m.port)
        msgType = type(msg)

        if (msgType = "roSGScreenEvent")
            if (msg.isScreenClosed())
                return
            end if
        else if (msgType = "roSystemLogEvent")
            sysLogEvent = msg.getInfo()
            datadogroku_ddLogInfo("Received system event: " + sysLogEvent.LogType)
            m.global.datadogRumAgent@.addResource(sysLogEvent)
        else if (msgType = "roInputEvent")
            datadogroku_ddLogInfo("Received input event" + msg.getInfo())
        end if
    end while

end sub
