' Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
' This product includes software developed at Datadog (https://www.datadoghq.com/).
' Copyright 2022-Today Datadog, Inc.

'----------------------------------------------------------------
' Main setup function.
' @return A configured TestSuite object.
'----------------------------------------------------------------
function TestSuite__FileUtils() as object
    this = BaseTestSuite()
    this.Name = "FileUtils"

    this.addTest("WhenMkDirs_ThenCreateDir", FileUtilsTest__WhenMkDirs_ThenCreateDir)
    this.addTest("WhenMkDirsExisting_ThenDoNothing", FileUtilsTest__WhenMkDirsExisting_ThenDoNothing)
    this.addTest("WhenMkDirs_ThenCreateParentDirs", FileUtilsTest__WhenMkDirs_ThenCreateParentDirs)
    this.addTest("WhenMkDirsInvalidPath_ThenDoNothing", FileUtilsTest__WhenMkDirsInvalidPath_ThenDoNothing)

    this.addTest("WhenMkParentDirs_ThenCreateDir", FileUtilsTest__WhenMkParentDirs_ThenCreateDir)
    this.addTest("WhenMkParentDirsExisting_ThenDoNothing", FileUtilsTest__WhenMkParentDirsExisting_ThenDoNothing)
    this.addTest("WhenMkParentDirs_ThenCreateParentDirs", FileUtilsTest__WhenMkParentDirs_ThenCreateParentDirs)
    this.addTest("WhenMkParentDirsInvalidPath_ThenDoNothing", FileUtilsTest__WhenMkParentDirsInvalidPath_ThenDoNothing)

    this.addTest("WhenTrackFolderPath_ThenComputePath", FileUtilsTest__WhenTrackFolderPath_ThenComputePath)

    this.addTest("WhenAppendAsciiFile_ThenAppendsToExistingFile", FileUtilsTest__WhenAppendAsciiFile_ThenAppendsToExistingFile)
    this.addTest("WhenAppendAsciiFile_ThenAppendsToEmptyFile", FileUtilsTest__WhenAppendAsciiFile_ThenAppendsToEmptyFile)

    this.addTest("WhenStrToLong_ThenReturnsLong", FileUtilsTest__WhenStrToLong_ThenReturnsLong)
    this.addTest("WhenStrToLongOnString_ThenReturnsLong", FileUtilsTest__WhenStrToLongOnString_ThenReturnsLong)

    return this
end function

'----------------------------------------------------------------
' Checks that mkDirs creates the required directory
'----------------------------------------------------------------
function FileUtilsTest__WhenMkDirs_ThenCreateDir() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(16)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkDirs(path)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkDirs does nothing if folder already exists
'----------------------------------------------------------------
function FileUtilsTest__WhenMkDirsExisting_ThenDoNothing() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(16)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkDirs(path)
    result2 = datadogroku_mkDirs(path)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(result2).isTrue("Expected result2 to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkDirs creates all intermediate directories
'----------------------------------------------------------------
function FileUtilsTest__WhenMkDirs_ThenCreateParentDirs() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    folderNames = IG_GetArray(["string", "string", "string", "string"])
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":"
    for each folderName in folderNames
        path = path + "/" + folderName
    end for
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkDirs(path)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkDirs fails silently when an invalid path is provided
' cf: https://developer.roku.com/en-gb/docs/developer-program/getting-started/architecture/file-system.md
'----------------------------------------------------------------
function FileUtilsTest__WhenMkDirsInvalidPath_ThenDoNothing() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    invalidChar = IG_GetOneOf(["<", ">", """", ":", "|", "?", "*"])
    path = IG_GetOneOf(["foo", "bar"]) + ":/" + IG_GetString(8) + invalidChar + IG_GetString(8)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkDirs(path)

    ' Then
    Assert.that(result).isFalse("Expected result to be false")
    Assert.that(fileSystem.Exists(path)).isFalse("Random folder at " + path + " shouldn't exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkParentDirs creates the required directory
'----------------------------------------------------------------
function FileUtilsTest__WhenMkParentDirs_ThenCreateDir() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(16)
    filePath = path + "/" + IG_GetString(16)
    if (fileSystem.Exists(filePath))
        return "Random folder at " + filePath + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkParentDirs(filePath)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    Assert.that(fileSystem.Exists(filePath)).isFalse("Random folder/file at " + filePath + " shouldn't exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkparentdirs does nothing if folder already exists
'----------------------------------------------------------------
function FileUtilsTest__WhenMkParentDirsExisting_ThenDoNothing() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(16)
    filePath = path + "/" + IG_GetString(16)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkParentDirs(filePath)
    result2 = datadogroku_mkparentdirs(filePath)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(result2).isTrue("Expected result2 to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    Assert.that(fileSystem.Exists(filePath)).isFalse("Random folder/file at " + filePath + " shouldn't exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkParentDirs creates all intermediate directories
'----------------------------------------------------------------
function FileUtilsTest__WhenMkParentDirs_ThenCreateParentDirs() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    folderNames = IG_GetArray(["string", "string", "string", "string"])
    path = IG_GetOneOf(["cachefs", "tmp"]) + ":"
    for each folderName in folderNames
        path = path + "/" + folderName
    end for
    filePath = path + "/" + IG_GetString(16)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkParentDirs(filePath)

    ' Then
    Assert.that(result).isTrue("Expected result to be true")
    Assert.that(fileSystem.Exists(path)).isTrue("Random folder at " + path + " should exist")
    Assert.that(fileSystem.Exists(filePath)).isFalse("Random folder/file at " + filePath + " shouldn't exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that mkParentDirs fails silently when an invalid path is provided
' cf: https://developer.roku.com/en-gb/docs/developer-program/getting-started/architecture/file-system.md
'----------------------------------------------------------------
function FileUtilsTest__WhenMkParentDirsInvalidPath_ThenDoNothing() as string
    ' Given
    fileSystem = CreateObject("roFileSystem")
    invalidChar = IG_GetOneOf(["<", ">", """", ":", "|", "?", "*"])
    path = IG_GetOneOf(["foo", "bar"]) + ":/" + IG_GetString(8) + invalidChar + IG_GetString(8)
    filePath = path + "/" + IG_GetString(16)
    if (fileSystem.Exists(path))
        return "Random folder at " + path + " shouldn't exist"
    end if

    ' When
    result = datadogroku_mkParentDirs(filePath)

    ' Then
    Assert.that(result).isFalse("Expected result to be false")
    Assert.that(fileSystem.Exists(path)).isFalse("Random folder at " + path + " shouldn't exist")
    Assert.that(fileSystem.Exists(filePath)).isFalse("Random folder/file at " + filePath + " shouldn't exist")
    return ""
end function

'----------------------------------------------------------------
' Checks that trackFolderPath creates a relevant path
'----------------------------------------------------------------
function FileUtilsTest__WhenTrackFolderPath_ThenComputePath() as string
    ' Given
    trackType = IG_GetString(8)
    version = IG_GetInteger(64)

    ' When
    result = datadogroku_trackFolderPath(trackType, version)

    ' Then
    Assert.that(result).isEqualTo("cachefs:/datadog/v" + version.toStr() + "/" + trackType)
    return ""
end function

'----------------------------------------------------------------
' Checks that AppendAsciiFile appends data to an existing file
'----------------------------------------------------------------
function FileUtilsTest__WhenAppendAsciiFile_ThenAppendsToExistingFile() as string
    ' Given
    filepath = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(8)
    initialText = IG_GetString(8)
    appendedText = IG_GetString(8)
    WriteAsciiFile(filepath, initialText)

    ' When
    result = datadogroku_AppendAsciiFile(filepath, appendedText)

    ' Then
    Assert.that(result).isEqualTo(true)
    Assert.that(ReadAsciiFile(filepath)).isEqualTo(initialText + appendedText)
    return ""
end function

'----------------------------------------------------------------
' Checks that AppendAsciiFile writes to an existing empty file
'----------------------------------------------------------------
function FileUtilsTest__WhenAppendAsciiFile_ThenAppendsToEmptyFile() as string
    ' Given
    filepath = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(8)
    appendedText = IG_GetString(8)
    WriteAsciiFile(filepath, "")

    ' When
    result = datadogroku_AppendAsciiFile(filepath, appendedText)

    ' Then

    Assert.that(result).isEqualTo(true)
    Assert.that(ReadAsciiFile(filepath)).isEqualTo(appendedText)
    return ""
end function

'----------------------------------------------------------------
' Checks that AppendAsciiFile creates and writes to a non-existing file
'----------------------------------------------------------------
function FileUtilsTest__WhenAppendAsciiFile_ThenCreateNewFile() as string
    ' Given
    filepath = IG_GetOneOf(["cachefs", "tmp"]) + ":/" + IG_GetString(8)
    appendedText = IG_GetString(8)

    ' When
    result = datadogroku_AppendAsciiFile(filepath, appendedText)

    ' Then
    Assert.that(result).isEqualTo(true)
    Assert.that(ReadAsciiFile(filepath)).isEqualTo(appendedText)
    return ""
end function

'----------------------------------------------------------------
' Checks that strToLong parses long properly
'----------------------------------------------------------------
function FileUtilsTest__WhenStrToLong_ThenReturnsLong() as string
    for i = 0 to 100
        ' Given
        input& = IG_GetLongInteger()
        input$ = input&.toStr()

        ' When
        result& = datadogroku_strToLong(input$)

        ' Then
        Assert.that(result&).isEqualTo(input&)
    end for
    return ""
end function

'----------------------------------------------------------------
' Checks that strToLong parses not long safely
'----------------------------------------------------------------
function FileUtilsTest__WhenStrToLongOnString_ThenReturnsLong() as string
    for i = 0 to 100
        ' Given
        input$ = IG_GetString(128)

        ' When
        crashed = false
        try
            datadogroku_strToLong(input$)
        catch e
            crashed = true
        end try

        ' Then
        Assert.that(crashed).isFalse()
    end for
    return ""
end function
